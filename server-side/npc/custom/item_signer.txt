//===== Hercules Script ======================================
//= Sign Your Items
//===== By: ==================================================
//= Lupus
//===== Current Version: =====================================
//= 1.1a
//===== Description: =========================================
//= Write you name on your rare equipment or weapon ^_-
//===== Additional Comments: =================================
//= 1.1 Cleaned and edited for general use. [Euphy]
//= 1.1a Added 'disable_items' command. [Euphy]
//============================================================

prt_in,24,61,7	script	Perchik	1_M_01,{
	setarray .@item[0],644,3;	// Item requirements: <ID>,<Count>{,...} (0 to disable)
	setarray .@cost[0],0,5000;	// Zeny requirements: <base price>,<price per refine>

	disable_items;
	mes "[Perchik]";
	mes "Eu posso ^0055FFassociar seu nome^000000 à praticamente qualquer item raro seu.";
	next;
	if(select("Me conte mais...", "Associe os items ao meu nome, por favor!") == 1) {
		mes "[Perchik]";
		mes "Eu consigo colocar seu nome em qualquer armadura ou arma sem slots.";
		emotion e_ic;
		next;
		mes "[Perchik]";
		if (getarraysize(.@item) || getarraysize(.@cost)) {
			mes "Para realizar meu trabalho eu aceito:";
			if (getarraysize(.@item)) for(set .@i,0; .@i<getarraysize(.@item); set .@i,.@i+2)
				mes " ~ "+.@item[.@i+1]+"x "+getitemname(.@item[.@i]);
			if (.@cost[0]) mes " ~ "+.@cost[0]+" Zeny";
			if (.@cost[1]) mes " ~ "+.@cost[1]+" Zeny per refine";
		}
		else mes "To trampando de graça, mas...";
		next;
		emotion e_cry;
		mes "[Perchik]";
		mes "Eu tenho minhas criança pra cuidar né parceiro";
		close;
	}
	mes "[Perchik]";
	mes "Mostra os itens que você quer associar ai mano...";
	next;
	setarray .@pos$[1],"Upper Headgear","Armor","Left hand","Right hand","Garment","Footgear","Left Accessory","Right Accessory","Mid Headgear","Lower Headgear";
	set .@menu$,"";
	for (set .@i,1; .@i<=10; set .@i,.@i+1) {
		if (getequipisequiped(.@i))
			set .@menu$, .@menu$+.@pos$[.@i]+" [^0055FF"+getequipname(.@i)+"^000000]";
		set .@menu$, .@menu$+":";
	}
	set .@part, select(.@menu$);
	set .@id, getequipid(.@part);
	set .@ref, getequiprefinerycnt(.@part);
	mes "[Perchik]";
	if (!getequipisequiped(.@part)) {
		mes "Nothing is equipped there!";
		emotion e_wah;
		close;
	}
	for(set .@i,0; .@i<4; set .@i,.@i+1)
		set .@slot[.@i], getequipcardid(.@part,.@i);
	if (.@slot[0]==255 || .@slot[0]==254 || .@slot[0]<0) {
		mes "Esse item ja ta associado, vacilão.";
		mes "Eu jamais estragaria o trabalho de um mestre.";
		emotion e_hmm;
		close;
	}
	if (.@slot[0]>4000 && .@slot[0]<5000) {
		mes "Uma carta? serio mesmo mano?!";
		mes "Como eu te falei eu nao mexo com isso.";
		emotion e_hmm;
		close;
	}
	if (getiteminfo(.@id, ITEMINFO_SLOTS) > 0) {
		mes "Ja falei que nao mexo com itens com slot né.";
		emotion e_sry;
		close;
	}
	getinventorylist;
	for(set .@i,0; .@i<@inventorylist_count; set .@i,.@i+1)
		if (@inventorylist_expire[.@i] != 0) {
			mes "Vish, não mexo com esses bagulho não";
			emotion e_hmm;
			close;
		}
	set .@price, .@cost[0]+(.@cost[1]*.@ref);
	if (getarraysize(.@item) || .@price) {
		mes "Eu vou precisar de:";
		if (getarraysize(.@item)) for(set .@i,0; .@i<getarraysize(.@item); set .@i,.@i+2) {
			mes " ~ "+.@item[.@i+1]+"x "+getitemname(.@item[.@i]);
			if (countitem(.@item[.@i]) < .@item[.@i+1]) set .@nr,1;
		}
		if (.@price) mes " ~ "+.@price+" Zeny";
	}
	mes "Vc quer mesmo que eu associe ^0055FF"+getitemname(.@id)+"^000000?";
	next;
	if(select("Obvio!", "Esquece")==2) {
		mes "[Perchik]";
		mes "Foi um prazer...";
		emotion e_yawn;
		close;
	}
	mes "[Perchik]";
	if (Zeny < .@price || .@nr) {
		mes "Eu nao trabalho de graça né.";
		emotion e_sry;
		close;
	}
	if (!countitem2(.@id,1,.@ref,0,.@slot[0],.@slot[1],.@slot[2],.@slot[3])) {
		mes "Cadê o "+getitemname(@id)+"...?";
		npctalk "To vendo que vc ta querendo me passar a perna!";
		logmes "Vacilão, tenta associar um item que não tem: "+getitemname(@id);
		emotion e_wah;
		close;
	}
	if (.@price) Zeny -= .@price;
	if (getarraysize(.@item))  for(set .@i,0; .@i<getarraysize(.@item); set .@i,.@i+2)
		delitem .@item[.@i], .@item[.@i+1];
	delitem2 .@id,1,1,.@ref,0,.@slot[0],.@slot[1],.@slot[2],.@slot[3];
	mes "Tudo Certinho!";
	emotion e_proud;
	getitem2 .@id,1,1,.@ref,0,254,0,getcharid(CHAR_ID_CHAR)&0xffff,(getcharid(CHAR_ID_CHAR)>>16)&0xffff;
	equip .@id;
	close;
}
